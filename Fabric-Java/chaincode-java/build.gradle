/*
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'application'
    id 'checkstyle'
    id 'jacoco'
}

group 'org.hyperledger.fabric.samples'
version '1.0-SNAPSHOT'

dependencies {

    implementation 'org.hyperledger.fabric-chaincode-java:fabric-chaincode-shim:2.5.+'
    implementation 'org.json:json:+'
    implementation 'com.owlike:genson:1.5'
    // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk15on
    implementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
    implementation(files("RefLib/jna-3.2.5.jar"))
    implementation(files("RefLib/jpbc-api-2.0.0.jar"))
    implementation(files("RefLib/jpbc-crypto-2.0.0.jar"))
    implementation(files("RefLib/jpbc-plaf-2.0.0.jar"))
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.assertj:assertj-core:3.25.3'
    testImplementation 'org.mockito:mockito-core:5.12.0'
}

repositories {
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

application {
    mainClass = 'org.hyperledger.fabric.contract.ContractRouter'
}

checkstyle {
    // toolVersion '8.21'
    // configFile file("config/checkstyle/checkstyle.xml")
    ignoreFailures = true
}

checkstyleMain {
    source ='src/main/java'
}

checkstyleTest {
    source ='src/test/java'
}

jacocoTestReport {
    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.9
            }
        }
    }

    finalizedBy jacocoTestReport
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

mainClassName = 'org.hyperledger.fabric.contract.ContractRouter'

shadowJar {
    archiveBaseName = 'chaincode'
    archiveVersion = ''
    archiveClassifier = ''
    mergeServiceFiles()

    manifest {
        attributes 'Main-Class': 'org.hyperledger.fabric.contract.ContractRouter'
    }
}

check.dependsOn jacocoTestCoverageVerification
installDist.dependsOn check
